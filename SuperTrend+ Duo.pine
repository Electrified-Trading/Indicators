// @version=5
indicator('SuperTrend+ Duo', overlay = true)
import Electrified/SupportResitanceAndTrend/3 as SRT
import Electrified/MomentumSignals/7 as MS 

ATR = 'Average True Range', ATR1 = 'Major', ATR2 = 'Minor', DISPLAY = 'Display'
WMA = 'WMA', EMA = 'EMA', SMA = 'SMA', VWMA = 'VWMA', VAWMA = 'VAWMA'
MOMENTUM = 'Stochastic RSI', VALUES = 'Values', LENGTHS = 'Lengths'

mode1       = input.string(VAWMA,   'Mode', group=ATR1, options=[SMA,EMA,WMA,VWMA,VAWMA], tooltip='Selects which averaging function will be used to determine the ATR value.')
atrPeriod1  = input.int(240,        'Period', group=ATR1, tooltip='The number of bars to use in calculating the ATR value.')
atrM1       = input.float(4.5,     'Multiplier', group=ATR1, step=0.5, tooltip='The multiplier used when defining the super trend limits.')
closeBars1  = input.int(4,          'Close Bars', group=ATR1, minval = 0, tooltip='The number of closed bars that have to exceed the super-trend value before the trend reversal is confirmed.')

mode2       = input.string(WMA,   'Mode', group=ATR2, options=[SMA,EMA,WMA,VWMA,VAWMA], tooltip='Selects which averaging function will be used to determine the ATR value.')
atrPeriod2  = input.int(60,        'Period', group=ATR2, tooltip='The number of bars to use in calculating the ATR value.')
atrM2       = input.float(2.0,      'Multiplier', group=ATR2, step=0.5, tooltip='The multiplier used when defining the super trend limits.')
closeBars2  = input.int(2,          'Close Bars', group=ATR2, minval = 0, tooltip='The number of closed bars that have to exceed the super-trend value before the trend reversal is confirmed.')

[stochRSI_signal, stochRSI_stage] = MS.stochRSI(
  input.int(6,  'K', group=MOMENTUM, inline=VALUES, minval=1),
  input.int(4,  'D', group=MOMENTUM, inline=VALUES, minval=1),
  input.int(48, 'RSI', group=MOMENTUM, inline=LENGTHS, minval=1),
  input.int(48, 'Stochastic', group=MOMENTUM, inline=LENGTHS, minval=1),    
  hlc3, WMA)

showsignals     = input(true, 'Show Buy/Sell Signals ?', group=DISPLAY)
highlighting    = input(true, 'Highlighter On/Off ?', group=DISPLAY)


// Plot Major
[trend1, up1, dn1, unconfirmed1, warn1, reversal1] = SRT.superTrend(atrM1, atrPeriod1, mode1, closeBars1)
up1Plot = plot(trend1==+1 ? up1 : na, 'Major Up Trend', style=plot.style_linebr, linewidth=2, color=unconfirmed1==0?color.green:color.yellow)
dn1Plot = plot(trend1==-1 ? dn1 : na, 'Major Down Trend', style=plot.style_linebr, linewidth=2, color=unconfirmed1==0?color.red:color.yellow)
plotshape(trend1 == +1 and trend1[1] == -1 ? up1 : na, 'UpTrend Begins', location=location.absolute, style=shape.circle, size=size.tiny, color=color.green)
plotshape(trend1 == -1 and trend1[1] == +1 ? dn1 : na, 'DownTrend Begins', location=location.absolute, style=shape.circle, size=size.tiny, color=color.red)

// Plot Minor
[trend2, up2, dn2, unconfirmed2, warn2, reversal2] = SRT.superTrend(atrM2, atrPeriod2, mode2, closeBars2)
plot(trend2==+1 ? up2 : na, 'Minor Up Trend', style=plot.style_linebr, linewidth=1, color=unconfirmed2==0?color.green:color.yellow)
plot(trend2==-1 ? dn2 : na, 'Minor Down Trend', style=plot.style_linebr, linewidth=1, color=unconfirmed2==0?color.red:color.yellow)

// Plot Signals
var phase = 0
if trend1 == +1 and trend1[1] != +1
    phase := +1
else if trend1 == -1 and trend1[1] != -1
    phase := -1

minorUp = trend2 > 0 and trend2[1] < 0
minorDn = trend2 < 0 and trend2[1] > 0

switch phase
    +1 =>
        if stochRSI_signal > +1
            phase := +3
        else if minorDn
            phase := +2
    -1 =>
        if stochRSI_signal < -1
            phase := -3
        else if minorUp
            phase := -2
    +2 =>
        if minorUp or stochRSI_signal > +1
            phase := +3
    -2 =>
        if minorDn or stochRSI_signal < -1
            phase := -3

upSignal = phase == +3 and phase[1] != +3
dnSignal = phase == -3 and phase[1] != -3
   
plotshape(upSignal and showsignals ? up2 : na, '▲', text='▲', location=location.absolute, style=shape.labelup, size=size.tiny, color=color.green, textcolor=color.white)
plotshape(dnSignal and showsignals ? dn2 : na, '▼', text='▼', location=location.absolute, style=shape.labeldown, size=size.tiny, color=color.red, textcolor=color.white)

// Highlight
mPlot = plot(ohlc4, '', style=plot.style_circles, linewidth=0)
longFillColor = color.new(highlighting ? (trend1 == 1 ? color.green : color.white) : color.white, 80)
shortFillColor = color.new(highlighting ? (trend1 == -1 ? color.red : color.white) : color.white, 80)
fill(mPlot, up1Plot, longFillColor, 'UpTrend Highligter')
fill(mPlot, dn1Plot, shortFillColor, 'DownTrend Highligter')

// Alerts
alertcondition(warn1 or reversal1,
  '1) Warning Major', message='SuperTrend+ Major Warning ({{ticker}} {{interval}})')
alertcondition(reversal1,
  '2) Reversal Major', message='SuperTrend+ Major Reversal ({{ticker}} {{interval}})')
alertcondition(warn2 or reversal2,
  '3) Warning Minor', message='SuperTrend+ Minor Warning ({{ticker}} {{interval}})')
alertcondition(reversal2,
  '4) Reversal Minor', message='SuperTrend+ Minor Reversal ({{ticker}} {{interval}})')
alertcondition(upSignal,
  '5) ▲ Up (+)', message='SuperTrend+ Duo ▲ Up (+) ({{ticker}} {{interval}})')
alertcondition(dnSignal,
  '6) ▼ Down (-)', message='SuperTrend+ Duo ▼ Down (-) ({{ticker}} {{interval}})')
